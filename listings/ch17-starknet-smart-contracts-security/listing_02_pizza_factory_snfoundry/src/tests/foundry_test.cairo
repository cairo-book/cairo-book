use source::pizza::{
    IPizzaFactoryDispatcher, IPizzaFactorySafeDispatcher, IPizzaFactoryDispatcherTrait,
    PizzaFactory, PizzaFactory::{Event as PizzaEvents, PizzaEmission}
};
//ANCHOR: import_internal
use source::pizza::PizzaFactory::{ownerContractMemberStateTrait, InternalTrait};
//ANCHOR_END: import_internal

use starknet::{ContractAddress, contract_address_const};

use snforge_std::{
    declare, ContractClassTrait, ContractClass, start_prank, stop_prank, CheatTarget, SpyOn,
    EventSpy, EventAssertions, spy_events, EventFetcher, load, cheatcodes::storage::load_felt252
};

fn owner() -> ContractAddress {
    contract_address_const::<'owner'>()
}

//ANCHOR: deployment
fn deploy_pizza_factory() -> (IPizzaFactoryDispatcher, ContractAddress) {
    let contract = declare("PizzaFactory").unwrap();
    let owner: ContractAddress = contract_address_const::<'owner'>();

    let mut constructor_calldata = array![owner.into()];

    let (contract_address, _) = contract.deploy(@constructor_calldata).unwrap();

    let dispatcher = IPizzaFactoryDispatcher { contract_address };

    (dispatcher, contract_address)
}
//ANCHOR_END: deployment

//ANCHOR: test_constructor
#[test]
fn test_constructor() {
    let (pizza_factory, pizza_factory_address) = deploy_pizza_factory();

    let pepperoni_count = load(pizza_factory_address, selector!("pepperoni"), 1);
    let pineapple_count = load(pizza_factory_address, selector!("pineapple"), 1);
    assert_eq!(pepperoni_count, array![10]);
    assert_eq!(pineapple_count, array![10]);
    assert_eq!(pizza_factory.get_owner(), owner());
}
//ANCHOR_END: test_constructor

//ANCHOR: test_owner
#[test]
fn test_change_owner_should_change_owner() {
    let (pizza_factory, pizza_factory_address) = deploy_pizza_factory();

    let new_owner: ContractAddress = contract_address_const::<'new_owner'>();
    assert_eq!(pizza_factory.get_owner(), owner());

    start_prank(CheatTarget::One(pizza_factory_address), owner());

    pizza_factory.change_owner(new_owner);

    assert_eq!(pizza_factory.get_owner(), new_owner);
}

#[test]
#[should_panic(expected: ("Only the owner can set ownership",))]
fn test_change_owner_should_panic_when_not_owner() {
    let (pizza_factory, pizza_factory_address) = deploy_pizza_factory();
    let not_owner = contract_address_const::<'not_owner'>();
    start_prank(CheatTarget::One(pizza_factory_address), not_owner);
    pizza_factory.change_owner(not_owner);
    stop_prank(CheatTarget::One(pizza_factory_address));
}
//ANCHOR_END: test_owner

//ANCHOR: test_make_pizza
#[test]
#[should_panic(expected: ("Only the owner can make pizza",))]
fn test_make_pizza_should_panic_when_not_owner() {
    let (pizza_factory, pizza_factory_address) = deploy_pizza_factory();
    let not_owner = contract_address_const::<'not_owner'>();
    start_prank(CheatTarget::One(pizza_factory_address), not_owner);

    pizza_factory.make_pizza();
}

#[test]
fn test_make_pizza_should_increment_pizza_counter() {
    // Setup
    let (pizza_factory, pizza_factory_address) = deploy_pizza_factory();
    start_prank(CheatTarget::One(pizza_factory_address), owner());
    let mut spy = spy_events(SpyOn::One(pizza_factory_address));

    // When
    pizza_factory.make_pizza();

    // Then
    let expected_event = PizzaEvents::PizzaEmission(PizzaEmission { counter: 1 });
    assert_eq!(pizza_factory.count_pizza(), 1);
    spy.assert_emitted(@array![(pizza_factory_address, expected_event)]);
}
//ANCHOR_END: test_make_pizza

//ANCHOR: test_internals
#[test]
fn test_set_as_new_owner_direct() {
    let mut state = PizzaFactory::contract_state_for_testing();
    let owner: ContractAddress = contract_address_const::<'owner'>();
    state.set_owner(owner);
    assert_eq!(state.owner.read(), owner);
}
//ANCHOR_END: test_internals
